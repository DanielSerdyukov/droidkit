apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def siteUrl = 'https://github.com/DanielSerdyukov/droidkit'
def issuesUrl = 'https://github.com/DanielSerdyukov/droidkit/issues'
def gitUrl = 'https://github.com/DanielSerdyukov/droidkit.git'
def bintrayDesc = 'DroidKit a carefully crafted Android framework'

group = 'droidkit'
version = rootProject.versionName

task androidJar(type: Jar, dependsOn: ['assembleRelease']) {
    group 'Build'
    from zipTree('build/intermediates/bundles/release/classes.jar')
    from zipTree("../processor/build/libs/processor-${version}.jar")
}

task androidSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives file("build/libs/library-${version}.jar")
    archives androidSourcesJar
}

publishing {
    publications {
        mavenAndroid(MavenPublication) {
            artifactId 'droidkit'
            pom {
                packaging 'jar'
            }
            artifact androidJar
            artifact androidSourcesJar {
                classifier 'sources'
            }
            pom.withXml {
                Node root = asNode()
                root.appendNode('name', 'droidkit')
                root.appendNode('description', bintrayDesc)
                root.appendNode('url', siteUrl)

                def issues = root.appendNode('issueManagement')
                issues.appendNode('system', 'github')
                issues.appendNode('url', issuesUrl)

                def scm = root.appendNode('scm')
                scm.appendNode('url', "scm:${siteUrl}")
                scm.appendNode('connection', "scm:${gitUrl}")
                scm.appendNode('developerConnection', "scm:${gitUrl}")

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    configurations = ['archives']
    publications = ['mavenAndroid']
    pkg {
        repo = 'droidkit'
        name = 'droidkit'
        desc = bintrayDesc
        websiteUrl = siteUrl
        issueTrackerUrl = issuesUrl
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        labels = ['android', 'framework']
    }
}