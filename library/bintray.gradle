apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

version = rootProject.version

task androidJar(type: Jar, dependsOn: ['assembleRelease']) {
    group 'Build'
    from zipTree('build/intermediates/bundles/release/classes.jar')
    from zipTree("../apt/build/libs/apt-${version}.jar")
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives file("build/libs/library-${version}.jar")
    archives sourcesJar
}

publishing {
    publications {
        mavenAndroid(MavenPublication) {
            groupId 'droidkit'
            artifactId 'droidkit'
            version rootProject.version
            artifact androidJar
            artifact sourcesJar { classifier 'sources' }
            pom.withXml {
                Node root = asNode()
                root.appendNode('name', 'droidkit')
                root.appendNode('description', 'DroidKit a carefully crafted Android framework.')
                root.appendNode('url', 'https://github.com/DanielSerdyukov/droidkit')

                def issues = root.appendNode('issueManagement')
                issues.appendNode('system', 'github')
                issues.appendNode('url', 'https://github.com/DanielSerdyukov/droidkit/issues')

                def scm = root.appendNode('scm')
                scm.appendNode('url', 'scm:https://github.com/DanielSerdyukov/droidkit')
                scm.appendNode('connection', 'scm:git@github.com:DanielSerdyukov/droidkit')
                scm.appendNode('developerConnection', 'scm:git@github.com:DanielSerdyukov/droidkit')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    configurations = ['archives']
    publications = ['mavenAndroid']
    pkg {
        repo = 'droidkit'
        name = 'droidkit'
        licenses = ['Apache-2.0']
    }
}